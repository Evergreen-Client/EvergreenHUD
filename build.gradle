plugins {
    id 'java'
    id 'net.minecraftforge.gradle.forge' version 'b13624b'
}

version = '2.0-RC.1.1'
group = 'co.uk.isxander'
archivesBaseName = 'evergreenhud'

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'

    replace('@BUILD_VER@', project.version)
}

dependencies {
    provided files('libs/modcore.jar')
    provided files('libs/xanderlib.jar')
}

jar {
    archiveName = "[$minecraft.version-$version] EvergreenHUD.jar"
    manifest {
        attributes (
                'FMLCorePlugin': 'co.uk.isxander.evergreenhud.forge.FMLLoadingPlugin',
                'ModSide': 'CLIENT',
                'FMLCorePluginContainsFMLMod': 'Yes, yes it does'
        )
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources