plugins {
    id 'java'

    id 'net.minecraftforge.gradle.forge' version '8708bf3e0'
    id 'org.spongepowered.mixin' version '0.6-SNAPSHOT'

    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

version = '2.0-RC.4'
group = 'co.uk.isxander'
archivesBaseName = 'EvergreenHUD'

targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'
    makeObfSourceJar = false

    replace '@BUILD_VER@', project.version
    clientRunArgs += '--tweakClass org.spongepowered.asm.launch.MixinTweaker'
    clientRunArgs += '--mixin mixins.evergreenhud.json'
}

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven {
        name = 'SpongePowered'
        url = 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    provided files('libs/modcore.jar')
    provided files('libs/xanderlib.jar')

    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

mixin {
    add sourceSets.main, 'mixins.evergreenhud.refmap.json'
}

String evergreenArchiveName = "EvergreenHUD (${minecraft.version}-${project.version})"

jar {
    archiveFileName = "${evergreenArchiveName}.jar"
    enabled = false

    manifest {
        attributes (
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.evergreenhud.json',
                'FMLAT': 'evergreenhud_at.cfg',
                'FMLCorePlugin': 'co.uk.isxander.evergreenhud.forge.FMLLoadingPlugin',
                'ModSide': 'CLIENT',
                'FMLCorePluginContainsFMLMod': true,
                'ForceLoadAsMod': true
        )
    }
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

// Forge does not import resources properly.
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}

tasks.moveResources.dependsOn processResources
tasks.classes.dependsOn moveResources

shadowJar {
    archiveBaseName.set(evergreenArchiveName)
    archiveVersion.set('')
    archiveClassifier.set('')

    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

reobfJar.dependsOn tasks.shadowJar