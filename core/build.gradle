apply plugin: 'java-library'

buildConfig {
    className('EvergreenInfo')
    packageName('dev.isxander.evergreenhud')
    useKotlinOutput()

    buildConfigField('String', 'MOD_NAME', "\"${mod_name}\"")
    buildConfigField('String', 'MOD_ID', "\"${mod_id}\"")
    buildConfigField('String', 'MOD_VERSION', "\"${project.version}\"")
    buildConfigField('String', 'MOD_REVISION', "\"${project.gitHash == null ? '???????' : project.gitHash}\"")
    buildConfigField('dev.isxander.evergreenhud.repo.ReleaseChannel', 'CHANNEL', "ReleaseChannel.${!core_prerelease.trim().isEmpty() ? 'BETA' : 'RELEASE'}")
}

repositories {
    maven { url = 'https://libraries.minecraft.net' }
}

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    api "org.jetbrains.kotlin:kotlin-reflect"

    api 'com.google.code.gson:gson:2.8.7'

    api 'dev.isXander.chachy-mc-api:keventbus-dsl:0.2.0'
    api 'com.github.KevinPriv:KEventBus:86cf2e023e'

    api 'org.reflections:reflections:0.9.12'
    api 'com.squareup.okhttp3:okhttp:4.9.1'

    // there is no api jar for Elementa or UniversalCraft so
    // depend on whatever version and exclude all dependencies
    // then in the other modules depend on them properly.
    // gross but I don't see any other way
    compileOnly ('gg.essential:Elementa:next-337-10809-SNAPSHOT') {
        exclude module: 'kotlin-stdlib-jdk8'
        exclude module: 'kotlin-reflect'
        exclude module: 'UniversalCraft'
        exclude module: 'minecraft'
        exclude module: 'yarn'
        exclude module: 'fabric-loader'
        exclude module: 'fabric-api'
        exclude module: 'forge'
    }
    compileOnly ('gg.essential:UniversalCraft:next-137-10809-SNAPSHOT') {
        exclude module: 'kotlin-stdlib-jdk8'
        exclude module: 'kotlin-reflect'
        exclude module: 'UniversalCraft'
        exclude module: 'minecraft'
        exclude module: 'yarn'
        exclude module: 'fabric-loader'
        exclude module: 'fabric-api'
        exclude module: 'forge'
    }
}

shadowJar {
    relocate 'com.google.gson', 'dev.isxander.gson'
}

jar {
    enabled = true
}

